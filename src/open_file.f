c     **********************************************************************
c
c      Copyright (c) 2007, 2019 Global Phasing Ltd.
c
c      This Source Code Form is subject to the terms of the Mozilla Public
c      License, v. 2.0. If a copy of the MPL was not distributed with this
c      file, You can obtain one at http://mozilla.org/MPL/2.0/.
c
c      Authors: Clemens Vonrhein and Gerard Bricogne
c
c     **********************************************************************

c     open file in the appropriate way

      subroutine open_file(filtyp)
c
      implicit none
c
      include 'xds.inc'
c
      character filtyp*13
c
      include 'args.inc'
      include 'units.inc'
c
      integer istat
      character line*1024
c
      integer iprint, ifail, nline
c
c----+=================================================================
c
      if (filnam.eq.' ') then
        call error_exit('variable filnam empty')
      end if
c
      open(lun,file=filnam,status='OLD',form='FORMATTED',iostat=istat)
      if (istat.ne.0) call error_exit('error opening file ' // filnam)
c
      filtyp = 'UNKNOWN'
      nline = 0
 10   read(lun,'(a)',iostat=istat) line
      if (istat.ne.0)
     +  call error_exit('error reading line from file ' // filnam)
      nline = nline + 1
c
c     get file format
c
      if (filtyp.eq.'UNKNOWN') then
        if (line(1:17).eq.'!FORMAT=XDS_ASCII') then
          filtyp = 'XDS_ASCII'
        else if (line(1:26).eq.'!OUTPUT_FILE=INTEGRATE.HKL') then
          filtyp = 'XDS_INTEGRATE'
        else if (line(1:3).eq.'MTZ') then
          filtyp = 'MTZ'
        end if
        if (filtyp.eq.'UNKNOWN') then
          call error_exit('cannot detect file type from first line = '
     .      //line)
        end if
        if (filtyp.eq.'XDS_ASCII') goto 10
      else
c
c       for XSCALE output we need to look further than the first line -
c       let's test up to line 10:
        if (filtyp.eq.'XDS_ASCII') then
          if (line(1:20).eq.'!Generated by XSCALE') then
            filtyp = 'XDS_XSCALE'
          else
            if (nline.lt.10) then
              goto 10
            end if
          end if
        end if
      end if
c
      if (iverb.gt.0) then
        write(stdout,'(/,2a,/)')
     +    ' NOTE: file type detected = ' // trim(adjustl(filtyp))
      end if
c
c     -----------------------------------------------------
c     MTZ file
c     -----------------------------------------------------
c
      if (filtyp.eq.'MTZ') then
        close(lun)
        lun = 1
        iprint = 0
        ifail = 0
        call mtzini
        call lropen(lun,filnam,iprint,ifail)
        if (ifail.ne.0) then
          call error_exit('error opening MTZ file ' // filnam)
        end if
c
c     -----------------------------------------------------
c     various ASCII files
c     -----------------------------------------------------
c
      else
        rewind(lun)
      end if
c
c----+=================================================================
c
      return
      end
